plugins {
    alias(libs.plugins.agp.lib)
    id('maven-publish')
    id('signing')
}

def publishingProps = new Properties()
def publishingFile = file("${rootDir}/keystore/publishing.properties")
if (publishingFile.exists()) {
    publishingProps.load(publishingFile.newInputStream())
}

android {
    namespace = 'com.rosan.dhizuku.api'
    compileSdk = TARGET_SDK

    defaultConfig {
        minSdk = MIN_SDK
        targetSdk = TARGET_SDK
    }

    buildFeatures {
        buildConfig = false
        aidl = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    compileOnly(libs.annotation)
    compileOnly(libs.core)
    compileOnly project(':hidden-api')
}

publishing {
    publications {
        mavenRelease(MavenPublication) {
            afterEvaluate {
                from components.release
            }

            group = publishingProps['groupId']
            artifactId = publishingProps['api.artifactId']
            version = API_VERSION

            pom {
                name = publishingProps['pom.name']
                description = publishingProps['pom.description']
                inceptionYear = publishingProps['pom.inceptionYear']
                url = publishingProps['pom.url']
                scm {
                    url = publishingProps['pom.scmUrl']
                    connection = publishingProps['pom.scmConnection']
                }
                licenses {
                    license {
                        name = publishingProps['pom.licenceName']
                        url = publishingProps['pom.licenceUrl']
                    }
                }
                developers {
                    developer {
                        name = publishingProps['pom.developerName']
                        url = publishingProps['pom.developerUrl']
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = uri(rootProject.layout.buildDirectory.dir("staging").get().asFile)
        }
    }
}

signing {
    useInMemoryPgpKeys(
            publishingProps['signing.keyId'],
            file(publishingProps['signing.secretKeyRingFile']).text,
            publishingProps['signing.password']
    )
    sign publishing.publications
}
